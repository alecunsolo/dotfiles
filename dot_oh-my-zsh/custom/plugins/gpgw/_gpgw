#compdef gpgw

_gpgw() {
    local curcontext="$curcontext" state line
    typeset -A opt_args

    _arguments -C \
        ":command:->command" \
        "*::options:->options"

    case $state in
        (command)
            local -a subcommads
            subcommands=(
                'decrypt:Decrypt file'
                'encrypt:Encrypt file or directory'
                'restart-agent:Restart gpg agent'
                'sign:Sign files'
                'verify:Verify gpg signagure'
            )
            _describe -t commands 'commands' subcommands
            ;;
        (options)
            case $line[1] in
                (decrypt)
                    _gpgw_decrypt
                    ;;
                (encrypt)
                    _gpgw_encrypt
                    ;;
                (verify)
                    _gpgw_verify
                    ;;
                (sign)
                    _gpgw_sign
                    ;;
            esac
    esac
}

# https://unix.stackexchange.com/questions/603077/zsh-completion-for-custom-script-complete-options-from-case-statement
_gpgw_encrypt() {
    _arguments \
        "1:file:_files" \
        {-o,--out}"[Output file]" \
        {-S,--no-sign}"[Don't sign encrypted file]" \
        {-a,--armor}"[ASCII armor]"
}

_gpgw_decrypt() {
    _arguments \
        "1:file:_files" \
        {-o,--out}"[Output file]"
}

_gpgw_verify() {
    _arguments "1:file:_files"
}

_gpgw_sign() {
    _arguments "1:file:_files"
}

_gpgw "$@"
